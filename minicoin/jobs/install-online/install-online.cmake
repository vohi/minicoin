if (WIN32)
  set(installer_file_os "windows-x86")
  set(installer_file_ext "exe")
  set(qt_package_arch "win64_msvc2019_64")
elseif(APPLE)
  set(installer_file_os "mac-x64")
  set(installer_file_ext "dmg")
  set(qt_package_arch "clang_64")
else()
  set(installer_file_os "linux-x64")
  set(installer_file_ext "run")
  set(qt_package_arch "gcc_64")
endif()

set(installer_version "4.0.0_alpha")

if(NOT PACKAGE)
  set(PACKAGE "preview.qt.qt6.60")
endif()
if (NOT QT_VERSION)
  set(QT_VERSION 6.0.0)
endif()
if (NOT INSTALL_ROOT)
  set(INSTALL_ROOT "Qt")
endif()

set(INSTALL_ROOT ${CMAKE_CURRENT_SOURCE_DIR}/${INSTALL_ROOT})

if(EXISTS ${INSTALL_ROOT}/${QT_VERSION})
  message(STATUS "Removing previous installation in ${INSTALL_ROOT}/${QT_VERSION}")
  file(REMOVE_RECURSE ${INSTALL_ROOT}/${QT_VERSION})
endif()

set(qt_base_url "http://download.qt.io/development_releases/online_installers")
set(installer_file "qt-unified-${installer_file_os}-${installer_version}-online.${installer_file_ext}")

if(DEFINED ENV{TEMP})
  set(TMPDIR $ENV{TEMP})
else()
  set(TMPDIR "/tmp")
endif()

message(STATUS "Downloading ${installer_file} from ${qt_base_url} to ${TMPDIR}/${installer_file}")
file(DOWNLOAD "${qt_base_url}/${installer_file}" ${TMPDIR}/${installer_file} SHOW_PROGRESS)
file(COPY ${TMPDIR}/${installer_file} DESTINATION . FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ)
file(REMOVE ${TMPDIR}/${installer_file})

message(STATUS "Running ${installer_file}")
execute_process(COMMAND ./${installer_file} install ${PACKAGE} --root ${INSTALL_ROOT} --accept-licenses --auto-answer telemetry-question=No,AssociateCommonFiletypes=No,OverwriteTargetDirectory=Yes)
